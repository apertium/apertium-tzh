Multichar_Symbols 

%<n%>
%<v%>

%<tv%>

%<s_sg1%>
%<s_sg2%>
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>
%<o_sg2%>
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%{s%}
%{w%}
%{k%}

@U.S_PRS.1@
@U.S_PRS.2@
@U.S_PRS.3@
@U.S_NBR.SG@
@U.S_NBR.PL@

LEXICON Root

Prefixes ; 
Miscellaneous ; 
Sustantivos ;
Conjunciones ; 
Determinantes ; 
Adverbios ; 

LEXICON Prefixes 

PERS-ERG-PREF ; 
POSS ; 

!LEXICON PERS-ABS-IV
!
!%<s_sg1%>:%>on # ; 
!%<s_sg2%>:%>at # ; 
!%<s_sg3%>:%> # ; 
!%<s_pl1%>:%>otik # ; 
!%<s_pl3%>:%> # ; 
!%<s_pl3%>:%>ik # ; 

LEXICON PERS-ABS-TV-1
@U.S_NBR.SG@%<o_sg1%>:@U.S_NBR.SG@%>on # ; 
@U.S_NBR.PL@%<o_sg1%>:@U.S_NBR.PL@%>on%>ik # ; 
%<o_pl1%>:%>otik # ; 

LEXICON PERS-ABS-TV-23

@U.S_NBR.SG@%<o_sg2%>:@U.S_NBR.SG@%>at # ; 
@U.S_NBR.PL@%<o_sg2%>:@U.S_NBR.PL@%>at%>ik # ; 
%<o_sg3%>:%> # ; 
%<o_pl2%>:%>ex # ; 

LEXICON PERS-ERG-PREF

@U.S_PRS.1@:@U.S_PRS.1@h%{k%}%> TV ; 
@U.S_PRS.2@:@U.S_PRS.2@a%{w%}%> TV ; 
@U.S_PRS.3@:@U.S_PRS.3@%{s%}%> TV ; 

LEXICON PERS-ERG-1-SUFF

@U.S_NBR.PL@%<s_pl1%>:@U.S_NBR.PL@%>tik PERS-ABS-TV-23 ; 
@U.S_NBR.SG@%<s_sg1%>:@U.S_NBR.SG@%>    PERS-ABS-TV-23 ; 

LEXICON PERS-ERG-2-SUFF

@U.S_NBR.SG@%<s_sg2%>:@U.S_NBR.SG@%>    PERS-ABS-TV-1 ; 
@U.S_NBR.PL@%<s_pl2%>:@U.S_NBR.PL@%>    PERS-ABS-TV-1 ; 

LEXICON PERS-ERG-3-SUFF

@U.S_NBR.SG@%<s_sg3%>:@U.S_NBR.SG@%>    PERS-ABS-TV-23 ; 
@U.S_NBR.PL@%<s_pl3%>:@U.S_NBR.PL@%>    PERS-ABS-TV-23 ; 

LEXICON PERS-ERG-SUFF 

@U.S_PRS.1@:@U.S_PRS.1@ PERS-ERG-1-SUFF ;
@U.S_PRS.2@:@U.S_PRS.2@ PERS-ERG-2-SUFF ;
@U.S_PRS.3@:@U.S_PRS.3@ PERS-ERG-3-SUFF ;

LEXICON POSS 
!j-nah “my house”
!‘a-nah “your house”
!s-nah “his house”

@U.P_PRS.1@:@U.P_PRS.1@j%> Sustantivos ; 
@U.P_PRS.2@:@U.P_PRS.2@a%> Sustantivos ; 
@U.P_PRS.3@:@U.P_PRS.3@s%> Sustantivos ; 

LEXICON V-TV 

%<v%>%<tv%>: PERS-ERG-SUFF ; 

LEXICON POSS-SUFF

@U.P_PRS.1@%<px1sg%>:@U.P_PRS.1@ # ; 
@U.P_PRS.2@%<px2sg%>:@U.P_PRS.2@ # ; 
@U.P_PRS.3@%<px3sg%>:@U.P_PRS.3@ # ; 
 

LEXICON N 

%<n%>: # ; 
%<n%>: POSS-SUFF  ; 
%<n%>%+e%<def%>:%>e # ; 

LEXICON CC

%<cnjcoo%>: # ; 

LEXICON ADV-ITG

%<adv%>%<itg%>: # ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Miscellaneous

ya%<impf%>:ya # ; 
la%<perf%>:la # ; 
bal%<qst%>:bal # ;
!ʼay ! "hay"
!maʼyuk ! "no hay"

ix%<adv%>:ix # ; ! "ya"

ta%<pr%>:ta # ; 

LEXICON Conjunciones

me:me CC ; ! "o"

LEXICON Determinantes

te%<det%>:te # ; 

LEXICON Sustantivos 

pim:pim N ; ! "grasa"
tat:tat N ; ! "padre"
bah:bah N ; ! ""
tʼul:tʼul N ; ! "conejo"
siʼ:siʼ N ; ! "leña"
puy:puy N ; ! "caracol"
waj:waj N ; ! "tortilla"
kʼahkʼ:kʼahkʼ N ; ! "fuego"
chʼahil:chʼahil N ; ! "humo"

ʼuh:ʼuh N ; ! "luna"

ʼechej:ʼechej N ; ! "axe"
sotsʼ:sotsʼ N ; ! "bat"
pohp:pohp N ; ! "mat"
!naht:naht N ; ! ""

nah:nah N ; ! "casa"

beh:beh N ; ! "camino"

pahay:pahay N ; ! "skunk" ! Dial/Bachajón 
paay:paay N ; ! "skunk" ! Dial/Oxchuc 
chojakʼ:chojakʼ N ; ! "red"

makom:makom N ; ! "blackberry"
chitam:chitam N ; ! "pig"
bahlam:bahlam N ; ! "tiger"
mahtan:mahtan N ; ! "gift"
kohtom:kohtom N ; ! "badger"

takʼin:takʼin N ; ! "dinero"
haʼ:haʼ N ; ! "agua"
ʼoxom:ʼoxom N ; ! "pot"
tulukʼ:tulukʼ N ; ! "turkey"
ʼixim:ʼixim N ; ! "corn"
tsʼunun:tsʼunun N ; ! "hummingbird"
jojochʼ:jojochʼ N ; ! "cornhusk"

LEXICON Adverbios 

banti:banti ADV-ITG ; ! "dónde"

LEXICON TV

tʼun:tʼun V-TV ; ! "seguir"
il:il V-TV ; ! "ver"


!i. ya    h-tʼun-at      I am following you                                   *
!iv. ya   h-tʼun-tik-0   We are following him                                 *
!v. ya    h-tʼun-tik-at  We are following you                                 * 
!vii. ya  h-tʼun-tik-ex  We are following you (pl.)                           * 
!ii. ya   a-tʼun-on      You are following me                                 *  
!vi. ya   a-tʼun-otik    You are following us /or/ you (pl.) are following us *  * 
!viii. ya a-tʼun-on-ik   You (pl.) are following me                           *
!iii. ya  s-tʼun-otik    He is following us        
!ix. ya   s-tʼun-at-ik   They are following you

!i. ya hk-il-at I see you
!ii. ya aw-il-on You see me
!iii. ya y-il-otik He sees us
!iv. ya hk-il-tik-0 We see him
!v. ya hk-il-tik-at We see you
!vi. ya aw-il-otik You see us or You (pl.) see us
!vii. ya hk-il-tik-ex We see you (pl.)
!viii. ya aw-il-on-ik You (pl.) see me
!ix. ya y-il-at-ik They see you 
